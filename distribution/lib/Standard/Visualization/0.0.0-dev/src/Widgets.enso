from Standard.Base import all
import Standard.Base.Errors.Common.Not_Invokable
import Standard.Base.Metadata.Widget
from Standard.Base.Logging import all
from Standard.Base.Meta import Instrumentor

from Standard.Table import all

from Standard.Database import all

## PRIVATE
   Basic preprocessor for widgets metadata visualization.

   Returns full annotation data for all requested arguments.
get_widget_json : Any -> Any -> Vector Text -> Text -> Text
get_widget_json value call_name argument_names uuids="{}" =
    other_args = uuids.parse_json

    cache (name_or_index:Text|Integer) -> Any =
        arg = other_args.get name_or_index.to_text
        case arg of
            uuid:Text -> Instrumentor.uuid uuid
            _ -> Nothing

    log_panic argument err =
        Widget.log_message "Failed for "+argument+": "+err.payload.to_display_text ..Warning
        Nothing

    read_annotation argument = Panic.catch Any handler=(log_panic argument) <|
        annotation = Warning.clear <| Meta.get_annotation value call_name argument
        return_target err = err.payload.target
        Panic.catch Not_Invokable handler=return_target
            annotation value cache=cache

    annotations = argument_names.map (arg -> [arg, read_annotation arg])
    annotations.to_json
